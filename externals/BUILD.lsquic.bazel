"""
lsquic bazel build file

lsquic v3.2.0
https://github.com/litespeedtech/lsquic/blob/v3.2.0/CMakeLists.txt
https://github.com/litespeedtech/lsquic/blob/v3.2.0/src/liblsquic/CMakeLists.txt
"""

# c++17 needed for sys/uio.h on windows
CC_OPTS = select({
    "@bazel_tools//src/conditions:windows": ["/std:c++17"],
    "//conditions:default": [],
})

cc_library(
    name = "lsquic",
    srcs = glob(["src/liblsquic/lsquic_*.c"]) + ["src/liblsquic/ls-qpack/lsqpack.c"],
    hdrs = glob([
        "include/*.h",
        "src/**/*.h",
        "src/liblsquic/common_*",
    ]),
    copts = CC_OPTS,
    defines = select({
        "@bazel_tools//src/conditions:windows": [
            "WIN32",
            "WIN32_LEAN_AND_MEAN",
            "NOMINMAX",
            "_CRT_SECURE_NO_WARNINGS",
        ],
        "//conditions:default": [],
    }),
    includes = [
        "include",
        "src",
        "src/liblsquic/ls-qpack",
        "src/liblsquic/ls-qpack/deps/xxhash",
        "src/lshpack",
        "src/lshpack/deps/xxhash",
    ] + select({
        "@bazel_tools//src/conditions:windows": ["./wincompat"],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
    deps = [
        ":ls_hpack_xxhash",
        ":ls_qpack_xxhash",
        "@boringssl",
        "@zlib",
    ],
)

cc_library(
    name = "ls_hpack_xxhash",
    srcs = ["src/lshpack/deps/xxhash/xxhash.c"],
    hdrs = ["src/lshpack/deps/xxhash/xxhash.h"],
    copts = CC_OPTS,
)

cc_library(
    name = "ls_qpack_xxhash",
    srcs = ["src/liblsquic/ls-qpack/deps/xxhash/xxhash.c"],
    hdrs = ["src/liblsquic/ls-qpack/deps/xxhash/xxhash.h"],
    copts = CC_OPTS,
)
